apply plugin: 'java'


version = '1.0.2'


compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

repositories {
    mavenCentral();
}

sourceSets {
    integrationTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime + sourceSets.test.runtimeClasspath
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.0.201403182114"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacoco/html"
    }
}

apply plugin: 'sonarqube-runner'

sonarRunner {
    sonarProperties {
        property "sonar.host.url", sonarHostUrl
        property "sonar.jdbc.url", sonarJdbcUrl
        property "sonar.jdbc.driverClassName", sonarJdbcDriverClassName
        property "sonar.jdbc.username", sonarJdbcUsername
        property "sonar.jdbc.password", sonarJdbcPassword
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7';
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.4.1';
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.4.1';
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.4.1';
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2';
    testCompile group: 'junit', name: 'junit', version: '4.11';
    testCompile group: 'org.easymock', name: 'easymock', version: '3.2';
}

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven';
    bintrayUsername = 'mharris021';
    bintrayRepository = 'darcstarsolutions';
    bintrayPackage = 'games'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar;
    archives javadocJar;
}

apply plugin: 'distribution'

distributions {
    main {
        baseName = archivesBaseName

        contents {
            from { libsDir }
        }
    }

}

apply plugin: 'maven-publish'

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            groupId 'com.darcstarsolutions.games'
            artifactId 'games-common'

            artifact sourcesJar
            artifact javadocJar

        }
    }

    repositories {
        maven {
            name 'Bintray';
            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"

            credentials {
                username = bintrayUsername;
                password = bintrayApiKey
            }
        }
    }

}